* Model developed by Helmut Sennewald  6/27/2004
* This TL431 model has been developed from the schematic in the datasheet.
* It agrees with most of the test circuits and covers
* Tempco, C-load stability, AC gain, reverse diode, noise, transient, Zout
* Version 1.1
*  
*         
*                 Ref   
*              Catho|de
*           Anode | |
*               | | |  
.SUBCKT TL431A A K R
Q1 3 2 1 0 NPN1 2.70
Q2 2 2 A 0 NPN1 1
R1 1 A 800 TC1=0.00035
R2 4 2 2.4k
R3 4 3 7.2k
R4 5 4 3.28k
Q3 6 3 A 0 NPN1 1
R5 7 6 4k
Q4 10 5 7 0 NPN1 1
Q5 K R 5 0 NPN1 1
R6 2 12 1k
Q6 11 12 A 0 NPN1 0.2
Q9 K 11 13 0 NPN1 2
Q10 K 14 A 0 NPN1 10
R10 14 A 10k
R9 13 14 150
R7 K 8 800
Q7 10 10 8 0 PNP1 1
Q8 11 10 9 0 PNP1 1
D2 A K D1
D1 A 11 D3
R8 K 9 800
C1 K 11 20p
C2 6 3 20p
D3 11 R D2
*
* adjust output voltage with Is
.MODEL NPN1 NPN(Is=0.8e-14 BF=100 VAF=100 TF=0.5e-9 RB=50 IKF=10m KF=1e-16  AF=1 RE=10)
.MODEL PNP1 PNP(Is=1e-14 BF=50 VAF=50 TF=1e-8 IKF=2m KF=1e-16 AF=1)
.MODEL D1 D(Is=1e-13 Rs=10 CJ0=20p)
.MODEL D2 D(Is=1e-13 Rs=10 CJ0=2p BV=5 IBV=10u)
.MODEL D3 D(Is=1e-13 Rs=10 CJ0=2p )
* This TC1 does the trick for the total tempco
.ends
