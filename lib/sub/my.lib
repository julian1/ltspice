*DIODES_INC_SPICE_MODEL ZXGD3111 version1
*SIMULATOR=SIMETRIX
*ORIGIN=DZSL_DPG_SU
*DATE=20May2016
*VERSION=1







*---------- DMN63D8L Spice Model ----------
.SUBCKT DMN63D8L 10 20 30 
*     TERMINALS:  D  G  S
M1 1 2 3 3  NMOS  L = 1E-006  W = 1E-006 
RD 10 1 1.906 
RS 30 3 0.001 
RG 20 2 62 
CGS 2 3 2.111E-011 
EGD 12 0 2 1 1 
VFB 14 0 0 
FFB 2 1  VFB 1 
CGD 13 14 5.5E-011 
R1 13 0 1 
D1 12 13  DLIM 
DDG 15 14  DCGD 
R2 12 15 1 
D2 15 0  DLIM 
DSD 3 10  DSUB 
.MODEL NMOS NMOS  LEVEL = 3  VMAX = 5.378E+005  ETA = 0.001  VTO = 1.592 
+ TOX = 6E-008  NSUB = 1E+016  KP = 0.5626  U0 = 400  KAPPA = 10 
.MODEL DCGD D  CJO = 1.249E-011  VJ = 0.7186  M = 0.6 
.MODEL DSUB D  IS = 1.685E-012  N = 1.251  RS = 0.2452  BV = 65  CJO = 6.828E-012  VJ = 0.6  M = 0.6 
.MODEL DLIM D  IS = 0.0001 
.ENDS
*Diodes DMN63D8L Spice Model v1.0 Last Revised 2015/7/3




*---------- DMP31D7L Spice Model ----------

* JA .SUBCKT DMP31D7L_P 10 20 30 
.SUBCKT DMP31D7L 10 20 30 
*     TERMINALS:  D  G  S
M1 1 2 3 3 PMOS L = 1E-006 W = 1E-006 
RD 10 1 0.39 
RS 30 3 1E-006 
RG 20 2 4.45 
CGS 2 3 1.705E-011 
EGD 12 30 2 1 1 
VFB 14 30 0 
FFB 2 1 VFB 1 
CGD 13 14 6.5E-011 
R1 13 30 1 
D1 13 12 DLIM 
DDG 14 15 DCGD 
R2 12 15 1 
D2 30 15 DLIM 
DSD 10 3 DSUB 
.MODEL PMOS PMOS LEVEL = 3 U0 = 400 VMAX = 1E+006 ETA = 0.001 
+ TOX = 6E-008 NSUB = 1E+016 KP = 1.131 KAPPA = 19.32 VTO = -2.377 
.MODEL DCGD D CJO = 7E-012 VJ = 0.8 M = 0.6 
.MODEL DSUB D IS = 2.459E-009 N = 1.767 RS = 0.05116 BV = 34.99 
+ CJO = 3E-011 VJ = 0.8 M = 0.6
.MODEL DLIM D IS = 0.0001 
.ENDS

*Diodes DMP31D7L Spice Model v1.0J Last Revised 2018/01/14




# JA https://www.onsemi.com/design/resources/technical-documentation?rpn=FDN360P
*FDN360P at Temp. Electrical Model
*-------------------------------------
* JA .SUBCKT FDN360P 20 10 30 50
.SUBCKT FDN360P 20 10 30
*20=DRAIN 10=GATE 30=SOURCE 50=VTEMP
* JA
Vtemp 50 0 {temperature}

Rg 10 11x 1
Rdu 12x 1 1u
M1 2 1 4x 4x DMOS L=1u W=1u
.MODEL DMOS PMOS(VTO=-1.8 KP=3.8
+THETA=0.08 VMAX=9.8E5 LEVEL=3)
Cgs 1 5x 250p
Rd 20 4 7E-3
Dds 4 5x DDS
.MODEL DDS D(M=4.67E-1 VJ=9.92E-1 CJO=160p)
Dbody 20 5x DBODY
.MODEL DBODY D(IS=1.01E-12 N=1.077822 RS=0.000933 TT=8.21n)
Ra 4 2 7E-3
Rs 5x 5 0.5m
Ls 5 30 0.5n
M2 1 8 6 6 INTER
E2 8 6 4 1 2
.MODEL INTER PMOS(VTO=0 KP=10 LEVEL=1)
Cgdmax 7 4 380p
Rcgd 7 4 10meg
Dgd 4 6 DGD
Rdgd 4 6 10meg
.MODEL DGD D(M=3.19E-1 VJ=9.44E-3 CJO=380p)
M3 7 9 1 1 INTER
E3 9 1 4 1 -2
*ZX SECTION
EOUT 4x 6x poly(2) (1x,0) (3x,0) 0 0 0 0 1
FCOPY 0 3x VSENSE 1
RIN 1x 0 1G
VSENSE 6x 5x 0
RREF 3x 0 10m
*TEMP SECTION
ED 101 0 VALUE {V(50,100)}
VAMB 100 0 25
EKP 1x 0 101 0 .027
*VTO SECTION
EVTO 102 0 101 0 .0012
EVT 11x 12x 102 0 1
*DIODE THERMO BREAKDOWN SECTION
EBL VB1 VB2 101 0  0.8
VBLK VB2 0 30
D DB1 20 DBLK
.MODEL DBLK D(IS=1E-14 CJO=.1p RS=.1)
EDB 0 DB1 VB1 0 1
.ENDS FDN360P
*FDN360P (Rev.B1 new process) 5/29/03 **ST




* JA https://www.onsemi.com/design/resources/technical-documentation?rpn=FDN337N
*FDN337N at Temp. Electrical Model
*-------------------------------------
* JA
* .SUBCKT FDN337N 20 10 30 50
.SUBCKT FDN337N 20 10 30
*20=DRAIN 10=GATE 30=SOURCE 50=VTEMP
* JA
Vtemp 50 0 {temperature}
Rg 10 11x 1
Rdu 12x 1 1u
M1 2 1 4x 4x DMOS L=1u W=1u
.MODEL DMOS NMOS(VTO=.85 KP=1.94E+1
+THETA=.25 VMAX=1.5E5 LEVEL=3)
Cgs 1 5x 387p
Rd 20 4 1.1E-2
Dds 5x 4 DDS
.MODEL DDS D(M=4.08E-1 VJ=6.51E-1 CJO=329p)
Dbody 5x 20 DBODY
.MODEL DBODY D(IS=6.52E-10 N=1.177132 RS=5.5E-2 TT=4.5n)
Ra 4 2 1.1E-2
Rs 5x 5 0.5m
Ls 5 30 0.5n
M2 1 8 6 6 INTER
E2 8 6 4 1 2
.MODEL INTER NMOS(VTO=0 KP=10 LEVEL=1)
Cgdmax 7 4 968p
Rcgd 7 4 10meg
Dgd 6 4 DGD
Rdgd 6 4 10meg
.MODEL DGD D(M=4.05E-1 VJ=2.54E-2 CJO=968p)
M3 7 9 1 1 INTER
E3 9 1 4 1 -2
*ZX SECTION
EOUT 4x 6x poly(2) (1x,0) (3x,0) 0 0 0 0 1
FCOPY 0 3x VSENSE 1
RIN 1x 0 1G
VSENSE 6x 5x 0
RREF 3x 0 10m
*TEMP SECTION
ED 101 0 VALUE {V(50,100)}
VAMB 100 0 25
EKP 1x 0 101 0 .028
*VTO TEMP SECTION
EVTO 102 0 101 0 .001
EVT 12x 11x 102 0 1
*DIODE THEMO BREAKDOWN SECTION
EBL VB1 VB2 101 0 .08
VBLK VB2 0 30
D 20 DB1 DBLK
.MODEL DBLK D(IS=1E-14 CJO=.1p RS=.1)
EDB DB1 0 VB1 0 1
.ENDS FDN337N
*FDN337N (Rev.B1) 6/23/06
*ST

** Released by: Online Design Tools, Texas Instruments Inc.
* Part: JFE2140
* Date: 16AUG2021
* Model Type: Generic (suitable for all analysis types)
* EVM Order Number: N/A 
* EVM Users Guide:  N/A 
* Datasheet: SLPS730
* Created with Shichman and Hodges JFET SPICE modeling
*
* Model Version: Final 1.0
*
*****************************************************************************
*
* Final 1.0
* Release to Web.
*****************************************************************************
* Model Usage Notes:
* The following parameters are modeled: 
*      Static, large and small signals, and noise parameters
*
* Use 2 Symbols of JFEx140 to match one unit of JFE2140 device.
*
*****************************************************************************
*$
.SUBCKT JFEx140 D G S VCH VCL
*****************************************************************************
.model DJFEx140 D Is=1e-12 Cjo=0.1pF Rs=190k BV=10
.model NJFEx140 NJF (BETA=14.24m BETATCE=-0.22 VTO=-975m VTOTC=0.45m LAMBDA=11.9m RD=10 RS=8 N=1 M=0.33 PB=0.705 IS=5.34f CGD=3.5p CGS=9.2p AF=1 KF=0.2e-18 FC=0.5)
*
J_J1         D G S NJFEx140 
D_D1         G D   DJFEx140
X_U1         D VCH VCL ESD_OUT_JFEx140
X_U2         G VCH VCL ESD_OUT_JFEx140
X_U3         S VCH VCL ESD_OUT_JFEx140
.ENDS JFEx140
*$
*
.SUBCKT ESD_OUT_JFEx140 OUT VCH VCL
.MODEL ESD_SW VSWITCH(RON=50 ROFF=1E12 VON=700E-3 VOFF=100E-3)
S1 VCH OUT OUT VCH ESD_SW
S2 OUT VCL VCL OUT ESD_SW
.ENDS  ESD_OUT_JFEx140
*$
*





*******************************************************
* Taiwan Semiconductor
* SS110
* 1A, 100V Schottky Barrier Rectifier
* Date: 2023-05-04
*******************************************************
.MODEL SS110 D
+ IS=17.007E-6
+ N=2.0865
+ RS=88.792E-3
+ IKF=74.643
+ CJO=87.422E-12
+ M=.52568
+ VJ=.59535
+ ISR=10.010E-21
+ NR=4.9950
+ BV=100
+ IBV=1E-4
+ TT=17.264E-9

*******************************************************

* https://www.mouser.com/catalog/additional/Taiwan_Semiconductor_SS12.txt

*******************************************************
* Taiwan Semiconductor
* SS12
* 1A, 20V Schottky Barrier Rectifier
* Date: 2023-05-04
*******************************************************
.MODEL SS12 D
+ IS=21.205E-9
+ N=.79397
+ RS=69.395E-3
+ IKF=184.68
+ CJO=193.71E-12
+ M=.50208
+ VJ=.49536
+ ISR=735.70E-9
+ NR=4.0874
+ BV=20
+ IBV=2E-4
+ TT=19.730E-9

*******************************************************



